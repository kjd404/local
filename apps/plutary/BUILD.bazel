package(default_visibility = ["//visibility:private"])
load("@pip//:requirements.bzl", "requirement")
load("//tools/sql:postgres_test.bzl", "postgres_pytest")

config_setting(
    name = "is_macos",
    constraint_values = ["@platforms//os:macos"],
)

config_setting(
    name = "ocr_backend_paddle_override",
    define_values = {"ocr_backend": "paddle"},
)

filegroup(
    name = "example_receipts",
    srcs = glob(["src/test/resources/examples/**"]),
    visibility = ["//visibility:public"],
)

filegroup(
    name = "plutary_sources",
    srcs = glob(["src/plutary/**/*.py"]),
    visibility = ["//visibility:public"],
)

py_library(
    name = "plutary_lib",
    srcs = glob(
        ["src/plutary/**/*.py"],
        exclude = ["src/plutary/ocr/**/*.py"],
    ),
    imports = ["src"],
    deps = [
        "//apps/plutary/src/plutary/ocr:ocr_lib",
        requirement("numpy"),
        requirement("einops"),
        requirement("ftfy"),
        requirement("imagesize"),
        requirement("Jinja2"),
        requirement("lxml"),
        requirement("opencv-contrib-python"),
        requirement("openpyxl"),
        requirement("premailer"),
        requirement("psycopg"),
        requirement("pyclipper"),
        requirement("pypdfium2"),
        requirement("regex"),
        requirement("scikit-learn"),
        requirement("shapely"),
        requirement("tiktoken"),
        requirement("tokenizers"),
        requirement("pillow"),
    ],
)

py_binary(
    name = "receipt_cli",
    srcs = ["src/plutary/cli/receipt_cli.py"],
    main = "src/plutary/cli/receipt_cli.py",
    imports = ["src"],
    deps = [":plutary_lib"],
    data = [
        ":example_receipts",
        "//apps/plutary:ocr_service_binary",
    ],
    visibility = ["//visibility:public"],
)

alias(
    name = "ocr_service_binary_default",
    actual = select({
        ":is_macos": "//apps/plutary/mac_vision_ocr:ocr_service",
        "//conditions:default": "//apps/plutary/src/plutary/ocr:paddle_ocr_service_bin",
    }),
)

alias(
    name = "ocr_service_binary",
    actual = select({
        ":ocr_backend_paddle_override": "//apps/plutary/src/plutary/ocr:paddle_ocr_service_bin",
        "//conditions:default": "//apps/plutary:ocr_service_binary_default",
    }),
    visibility = ["//visibility:public"],
)

postgres_pytest(
    name = "plutary_tests",
    sql_dir = "ops/sql/plutary",
    env_prefix = "PLUTARY",
    history_table = "flyway_plutary_schema_history",
    pytest_args = ["apps/plutary/tests"],
    data = glob([
        "tests/**/*.py",
        "src/test/resources/**/*",
    ]) + [
        "//ops/sql/plutary:sql_files",
        "//apps/plutary/tests/ocr/golden:sample_receipt_photo_01_response_paddle.json",
        "//apps/plutary/tests/ocr/golden:sample_receipt_photo_01_response_vision.json",
        "//apps/plutary:ocr_service_binary",
    ],
    deps = [
        ":plutary_lib",
        requirement("numpy"),
        requirement("pillow"),
    ],
    visibility = ["//visibility:public"],
)
